localrules: star_idx
rule star_idx:
    input:
        ref_fa=lambda wc: config["data_paths"]["references"][wc.reference]["fasta"],
        ref_gtf=lambda wc: config["data_paths"]["references"][wc.reference]["gtf"],
    log:
        "tmp/star_ref/{reference}.log"
    output:
        directory("tmp/star_ref/{reference}")
    conda: "../envs/star.yml"
    resources:
        runtime=60,
        mem_gb=32,
    threads: 8
    shell:
        "( STAR"
        "   --runThreadN {threads}"
        "   --runMode genomeGenerate"
        "   --genomeDir {output}"
        "   --genomeSAindexNbases 12"
        "   --genomeFastaFiles {input.ref_fa}"
        "   --sjdbGTFfile {input.ref_gtf}"
        "   --sjdbOverhang 149"
        ") &>{log}"
        

rule split_pair_sample:
    input:
        reads=lambda wc: [f"tmp/reads/qc/{run}~{lib}_r12.fastq.gz"
                          for run, lib in s2rl[wc.sample]],
    output:
        r1="tmp/reads/samples/{sample}_R1.fastq.gz",
        r2="tmp/reads/samples/{sample}_R2.fastq.gz",
        se="tmp/reads/samples/{sample}_se.fastq.gz",
        yml="tmp/reads/samples/{sample}_split.stats.yml",
    log:
        "tmp/reads/samples/{sample}_split.log",
    resources:
        runtime=180,
        mem_gb=1,
        disk_gb=1,
    threads: 8
    conda: "../envs/reads.yml"
    params:
        ziplevel=8,
    shell:
        "(seqhax pairs"
        "   -1 >(pigz -p {threads} -{params.ziplevel} >{output.r1})"
        "   -2 >(pigz -p {threads} -{params.ziplevel} >{output.r2})"
        "   -u >(pigz -p {threads} -{params.ziplevel} >{output.se})"
        "   -l 64"
        "   -y {output.yml}"
        "   <(zcat {input})"
        " ) &>{log}"


rule star:
    input:
        genodir="tmp/star_ref/{reference}",
        r1="tmp/reads/samples/{sample}_R1.fastq.gz",
        r2="tmp/reads/samples/{sample}_R2.fastq.gz",
    output:
        bam="tmp/star/{reference}/{sample}.bam",
        bai="tmp/star/{reference}/{sample}.bam.bai",
    resources:
        runtime=600,
        mem_gb=32,
    threads: 8
    log: "tmp/star/{reference}/{sample}.bam.log",
    params:
        prefix= lambda wc, input, output: output.bam.replace("bam", "")
    conda: "../envs/star.yml"
    shell:
        "( STAR"
        "   --readFilesIn {input.r1} {input.r2}"
        "   --readFilesCommand zcat"
        "   --genomeDir  {input.genodir}"
        "   --outSAMtype BAM SortedByCoordinate"
        "   --outFileNamePrefix {params.prefix}"
        "   --runThreadN {threads}"
        " && mv  {params.prefix}Aligned.sortedByCoord.out.bam {output.bam}"
        " && samtools index {output.bam}"
        ") &>{log}"

#rule RG:
#    input:
#        bam="tmp/star/{reference}/{sample}.Aligned.sortedByCoord.out.bam",
#    output:
#        bam=temp("tmp/star/{reference}/{sample}.rg.bam"),
#        bai=temp("tmp/star/{reference}/{sample}.rg.bam.bai"),
#    resources:
#        runtime=30,
#        mem_gb=4,
#    threads: 1
#    log: "tmp/star/{reference}/{sample}.rg.bam.log",
#    conda: "../envs/star.yml"
#    shell:
#        "(samtools addreplacerg "
#        "   --no-PG"
#        "   -u"
#        "   -w"
#        "   -r '@RG\tID:{wildcards.sample}\tSM:{wildcards.sample}\tPL:ILLUMINA'"
#        "   -o {output.bam}"
#        " && samtools index {output.bam}"
#        ")&>{log}"
#



localrules: subread_idx
rule subread_idx:
    input:
        ref_fa=lambda wc: config["data_paths"]["references"][wc.reference]["fasta"],
    output:
        expand("tmp/subread_ref/{{reference}}{ext}",
                ext=[".00.b.array", ".00.b.tab", ".files", ".reads"]),
    log:
        "tmp/subread_ref/{reference}.snake.log",
    resources:
        runtime=30,
        mem_gb=8,
    params:
        prefix=lambda wc: f"tmp/subread_ref/{wc.reference}",
    threads: 1
    conda: "../envs/subread.yml"
    shell:
        "( subread-buildindex -FB -o {params.prefix} {input.ref_fa} ) &>{log}"

rule subjunc:
    input:
        ref="tmp/subread_ref/{reference}.files",
        r1="tmp/reads/samples/{sample}_R1.fastq.gz",
        r2="tmp/reads/samples/{sample}_R2.fastq.gz",
        ref_gtf=lambda wc: config["data_paths"]["references"][wc.reference]["gtf"],
    output:
        bam="tmp/subjunc/{reference}/{sample}.bam",
        bai="tmp/subjunc/{reference}/{sample}.bam.bai",
    resources:
        runtime=360,
        mem_gb=24,
    threads: 64
    log: "tmp/subjunc/{reference}/{sample}.bam.log",
    params:
        refprefix=lambda wc, input: input.ref.replace(".files", ""),
    conda: "../envs/subread.yml"
    shell:
        "(subjunc "
        "   -i {params.refprefix}"
        "   -r {input.r1}"
        "   -R {input.r2}"
        "   -a {input.ref_gtf}"
        "   -T {threads}"
        "   --multiMapping"
        #"   -B 10"
        #"   -M 6" # 6 mismatches
        "   --rg 'ID:{wildcards.sample}'"
        "   --rg 'SM:{wildcards.sample}'"
        "   --rg 'PL:ILLUMINA'"
        "   -o {output.bam}"
        "   -S fr"
        "   --allJunctions"
        "   --gtfFeature exon"
        "   --gtfAttr gene_id"
        "   --sortReadsByCoordinates"
        ") &>{log}"


localrules: onebam_fofn
rule onebam_fofn:
    input:
        bam=lambda wc: [
            f"tmp/{wc.aligner}/{wc.reference}/{sample}.bam"
            for sample in s2rl
        ],
        bai=lambda wc: [
            f"tmp/{wc.aligner}/{wc.reference}/{sample}.bam.bai"
            for sample in s2rl
        ],
    output:
        bamlist=temp("tmp/{aligner}/{reference}.bamlist")
    run:
        with open(output[0], "w") as fh:
            for bam in input.bam:
                print(bam, file=fh)


rule onebam:
    input:
        bam=lambda wc: [
            f"tmp/{wc.aligner}/{wc.reference}/{sample}.bam"
            for sample in s2rl
        ],
        bai=lambda wc: [
            f"tmp/{wc.aligner}/{wc.reference}/{sample}.bam.bai"
            for sample in s2rl
        ],
        bamlist="tmp/{aligner}/{reference}.bamlist",
    output:
        bam=temp("tmp/{aligner}/{reference}.bam"),
        bai=temp("tmp/{aligner}/{reference}.bam.bai"),
    resources:
        runtime=600,
        mem_gb=4,
    threads: 64
    log: "tmp/{aligner}/{reference}.bam.log",
    conda: "../envs/star.yml"
    shell:
        "(samtools merge "
        "   -@ {threads}"
        "   -1"
        "   -f"
        "   -o {output.bam}"
        "   -b {input.bamlist}"
        " && samtools index {output.bam}"
        ")&>{log}"
